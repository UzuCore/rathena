//===== rAthena Script ======================================= 
//= 아인브로흐 공항 직원
//===== By: ================================================== 
//= L0ne_W0lf, Muad_Dib
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Einbroch Airport Staff
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

airport,143,43,5	script	Airport Staff#airport1a::airport1	90,{
	mes "[Airport Staff]";
	mes "Welcome to the Einbroch Airport, where we offer nonstop flights to the cities of Juno, Lighthalzen and Hugel.";
	next;
	if (select("Board the Airship:Cancel") == 1) {
		mes "[Airport Staff]";
		mes "The Airship boarding fee is 1,200 zeny, but if you've got a Free Ticket for Airship, the fee will be waived.";
		mes "Will you board the Airship?";
		next;
		if (select("Yes:No") == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1; //Free_Flying_Ship_Ticket
				warp "airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "airport",148,51;
				end;
			}
			mes "[Airport Staff]";
			mes "I'm sorry, but you don't have a Free Ticket for Airship and you don't have enough zeny for boarding the Airship.";
			mes "Remember, the boarding fee is 1,200 zeny.";
			close;
		}
	}
	mes "[Airport Staff]";
	mes "Thank you and";
	mes "have a nice day.";
	close;
}

airport,158,43,5	duplicate(airport1)	Airport Staff#airport1b	90
airport,126,43,5	duplicate(airport1)	Airport Staff#airport1c	90

airport,143,49,3	script	Arrival Staff#airport2a::airport2	90,{
	mes "[Arrival Staff]";
	mes "Welcome to Einbroch Airport.";
	mes "If you are arriving from your flight, let me guide you to the main terminal.";
	mes "Otherwise, please board the Airship to depart to Juno, Lighthalzen and Hugel.";
	next;
	if (select("Exit to main terminal.:Cancel.") == 1) {
		mes "[Arrival Staff]";
		mes "Once you're in the main terminal, you will need to pay the fee again to board an Airship.";
		mes "You should only exit if the city of Einbroch is your intended destination.";
		mes "Proceed to the main terminal?";
		next;
		if (select("Yes:No") == 1) {
			warp "airport",142,40;
			end;
		}
	}
	mes "[Arrival Staff]";
	mes "Alright, thank you for your patronage and I hope you have a pleasant flight~";
	close;
}

airport,126,51,3	duplicate(airport2)	Arrival Staff#airport2b	90
airport,158,50,3	duplicate(airport2)	Arrival Staff#airport2c	90

einbroch,94,267,3	script	Airship Staff#ein01	91,{
	mes "[Airship Staff]";
	mes "Welcome to the Einbroch Airport.";
	mes "Please use this door to board the Airship which stops over Juno, Lighthalzen and Hugel in the Schwarzwald Republic.";
	next;
	mes "[Airship Staff]";
	mes "Otherwise, if Einbroch is your intended destination, please head down the stairs and ask the Arrival Staff to lead you to the main terminal.";
	mes "Thank you, and enjoy your travels.";
	close;
}
